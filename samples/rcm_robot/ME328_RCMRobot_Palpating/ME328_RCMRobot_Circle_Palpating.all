   (1) % SIMULATION DETAILS
   (2) % ==================
   (3) % NAME: ME 328, Remote Center of Motion Robot, Palpating
   (4) % AUTHOR: Fidel Hernandez
   (5) % DATE: April 30, 2012
   (6) %SetWorkingDirectory ME328Homework3/ME328_RCMRobot_Palpating
   (7) %
   (8) % SIMULATION DESCRIPTION
   (9) % ======================
   (10) % In this simulation, we control a remote center of motion robot so that
   (11) % its end effector simulates palpating liver tissue via approximation
   (12) % with a sine wave.
   (13) %
   (14) % FRAMES, BODIES, AND POINTS
   (15) % ==========================
   (16) NewtonianFrame N   % Newtonian/Global/Ground Frame, with Point No
   (17) RigidFrame     A   % Intermediate Frame, with Point Ao
   (18) RigidBody      B   % Link 1, Cylinder, with Points Bo, Bcm
   (19) RigidBody      C   % Link 2, Cylinder, with Points Co, Ccm
   (20) RigidBody      D   % Link 3, Cylinder, with Points Do, Dcm
   (21) RigidBody      E   % Link 4, Cylinder, with Points Eo, Ecm
   (22) Point       BC(B)  % Revolute joint point between B/C (fixed on B) 
   (23) Point       CD(C)  % Revolute joint point between C/D (fixed on C)
   (24) Point      Eff(E)  % End-effector (fixed on E)
   (25) Point      RCM(N)  % Remote center of motion (fixed on N)
   (26) %
   (27) % DYNAMICS VARIABLES AND CONSTANTS
   (28) % ================================
   (29) Variable     qx''  % Angle from Nz> to Az> about +Nx>
   (30) Variable     qy''  % Angle from Az> to Bz> about -Ay>
   (31) Variable     dz''  % -Bz> measure of Eff from Rcm
   (32) Constant    rLink  % Radius of links B, C, D, and E
   (33) Constant    lLink  % Length of links B, C, D, and E
   (34) Specified      Tx  % +Nx> measure of torque on A from N, motor
   (35) Specified      Ty  % -Ay> measure of torque on B from A, motor
   (36) Specified      Fz  % -Bz> measure of force on Ecm from Dcm, motor
   (37) %
   (38) % MASS AND INERTIA
   (39) % ================
   (40) Constant density
   (41) m = density*pi*rLink^2*lLink
-> (42) m = 3.141593*density*lLink*rLink^2

   (43) B.SetMass( m )
   (44) C.SetMass( m )
   (45) D.SetMass( m )
   (46) E.SetMass( m )
   (47) I = 1/2*m*rLink^2                  % Moment of inertia, long axis
-> (48) I = 0.5*rLink^2*m

   (49) J = 1/12*m*( 3*rLink^2+lLink^2 )   % Moment of inertia, transverse axis
-> (50) J = 0.08333333*(lLink^2+3*rLink^2)*m

   (51) B.SetInertia( Bcm, J, J, I )       % Inertia (about Bcm, bx>, by>, bz>)
   (52) C.SetInertia( Ccm, I, J, J )       % Inertia (about Ccm, ax>, ay>, az>)
   (53) D.SetInertia( Dcm, J, J, I )       % Inertia (about Dcm, bx>, by>, bz>)
   (54) E.SetInertia( Ecm, J, J, I )       % Inertia (about Ecm, bx>, by>, bz>)
   (55) %
   (56) % ROTATIONAL KINEMATICS
   (57) % =====================
   (58) A.RotatePositiveX( N, qx )
-> (59) A_N = [1, 0, 0; 0, cos(qx), sin(qx); 0, -sin(qx), cos(qx)]
-> (60) w_A_N> = qx'*Ax>
-> (61) alf_A_N> = qx''*Ax>

   (62) B.RotateNegativeY( A, qy )      
-> (63) B_A = [cos(qy), 0, sin(qy); 0, 1, 0; -sin(qy), 0, cos(qy)]
-> (64) w_B_A> = -qy'*By>
-> (65) w_B_N> = cos(qy)*qx'*Bx> - qy'*By> - sin(qy)*qx'*Bz>
-> (66) alf_B_A> = -qy''*By>
-> (67) alf_B_N> = (cos(qy)*qx''-sin(qy)*qx'*qy')*Bx> - qy''*By> + (-cos(qy)*
        qx'*qy'-sin(qy)*qx'')*Bz>

   (68) C.RotatePositiveZ( A, 0 )          % C is always parallel to A
-> (69) C_A = [1, 0, 0; 0, 1, 0; 0, 0, 1]
-> (70) w_C_A> = 0>
-> (71) w_C_N> = qx'*Cx>
-> (72) alf_C_A> = 0>
-> (73) alf_C_N> = qx''*Cx>

   (74) D.RotatePositiveZ( B, 0 )          % D is always parallel to B
-> (75) D_B = [1, 0, 0; 0, 1, 0; 0, 0, 1]
-> (76) w_D_B> = 0>
-> (77) w_D_N> = cos(qy)*qx'*Dx> - qy'*Dy> - sin(qy)*qx'*Dz>
-> (78) alf_D_B> = 0>
-> (79) alf_D_N> = (cos(qy)*qx''-sin(qy)*qx'*qy')*Dx> - qy''*Dy> + (-cos(qy)*
        qx'*qy'-sin(qy)*qx'')*Dz>

   (80) E.RotatePositiveZ( D, 0 )          % Prismatic joint between D and E
-> (81) E_D = [1, 0, 0; 0, 1, 0; 0, 0, 1]
-> (82) w_E_D> = 0>
-> (83) w_E_N> = cos(qy)*qx'*Ex> - qy'*Ey> - sin(qy)*qx'*Ez>
-> (84) alf_E_D> = 0>
-> (85) alf_E_N> = (cos(qy)*qx''-sin(qy)*qx'*qy')*Ex> - qy''*Ey> + (-cos(qy)*
        qx'*qy'-sin(qy)*qx'')*Ez>

   (86) %
   (87) % TRANSLATIONAL KINEMATICS
   (88) % ========================
   (89) Bcm.Translate( No, lLink/2*bz> )   % Bcm, measured from origin, No
-> (90) p_No_Bcm> = 0.5*lLink*Bz>
-> (91) v_Bcm_N> = -0.5*lLink*qy'*Bx> - 0.5*lLink*cos(qy)*qx'*By>
-> (92) a_Bcm_N> = -0.5*lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + 0.5*lLink*(2*
        sin(qy)*qx'*qy'-cos(qy)*qx'')*By> - 0.5*lLink*(qy'^2+cos(qy)^2*qx'^2)*
        Bz>

   (93) BC.Translate( No, lLink*bz> )	   % BC, measured from origin, No
-> (94) p_No_BC> = lLink*Bz>
-> (95) v_BC_N> = -lLink*qy'*Bx> - lLink*cos(qy)*qx'*By>
-> (96) a_BC_N> = -lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + lLink*(2*sin(qy)*
        qx'*qy'-cos(qy)*qx'')*By> - lLink*(qy'^2+cos(qy)^2*qx'^2)*Bz>

   (97) Ccm.Translate( BC, lLink/2*ax> )   % Ccm, measured from BC
-> (98) p_BC_Ccm> = 0.5*lLink*Ax>
-> (99) v_Ccm_N> = -lLink*qy'*Bx> - lLink*cos(qy)*qx'*By>
-> (100) a_Ccm_N> = -lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + lLink*(2*sin(qy)*
         qx'*qy'-cos(qy)*qx'')*By> - lLink*(qy'^2+cos(qy)^2*qx'^2)*Bz>

   (101) CD.Translate( BC, lLink*ax> )      % CD, measured from BC
-> (102) p_BC_CD> = lLink*Ax>
-> (103) v_CD_N> = -lLink*qy'*Bx> - lLink*cos(qy)*qx'*By>
-> (104) a_CD_N> = -lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + lLink*(2*sin(qy)*
         qx'*qy'-cos(qy)*qx'')*By> - lLink*(qy'^2+cos(qy)^2*qx'^2)*Bz>

   (105) RCM.Translate( No, lLink*nx> )     % RCM, measured from origin, No
-> (106) p_No_RCM> = lLink*Nx>
-> (107) v_RCM_N> = 0>
-> (108) a_RCM_N> = 0>

   (109) Dcm.Translate( RCM, lLink/2*bz> )  % Dcm, measured from RCM
-> (110) p_RCM_Dcm> = 0.5*lLink*Bz>
-> (111) v_Dcm_N> = -0.5*lLink*qy'*Bx> - 0.5*lLink*cos(qy)*qx'*By>
-> (112) a_Dcm_N> = -0.5*lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + 0.5*lLink*(2*
         sin(qy)*qx'*qy'-cos(qy)*qx'')*By> - 0.5*lLink*(qy'^2+cos(qy)^2*qx'^2)*
         Bz>

   (113) Eff.Translate( RCM, -dz*bz> )      % End effector, measured from RCM
-> (114) p_RCM_Eff> = -dz*Bz>
-> (115) v_Eff_N> = dz*qy'*Bx> + dz*cos(qy)*qx'*By> - dz'*Bz>
-> (116) a_Eff_N> = (2*dz'*qy'+dz*sin(qy)*cos(qy)*qx'^2+dz*qy'')*Bx> + (2*cos(
         qy)*dz'*qx'+dz*cos(qy)*qx''-2*dz*sin(qy)*qx'*qy')*By> + (dz*qy'^2+dz*
         cos(qy)^2*qx'^2-dz'')*Bz>

   (117) Ecm.Translate( Eff, lLink/2*bz> )  % Ecm, measured from end effector
-> (118) p_Eff_Ecm> = 0.5*lLink*Bz>
-> (119) v_Ecm_N> = -0.5*(lLink-2*dz)*qy'*Bx> - 0.5*cos(qy)*(lLink-2*dz)*qx'*
         By> - dz'*Bz>
-> (120) a_Ecm_N> = (2*dz'*qy'+dz*sin(qy)*cos(qy)*qx'^2+dz*qy''-0.5*lLink*sin(
         qy)*cos(qy)*qx'^2-0.5*lLink*qy'')*Bx> + (2*cos(qy)*dz'*qx'+lLink*sin(
         qy)*qx'*qy'+dz*cos(qy)*qx''-2*dz*sin(qy)*qx'*qy'-0.5*lLink*cos(qy)*qx
         '')*By> + (dz*qy'^2+dz*cos(qy)^2*qx'^2-0.5*lLink*(qy'^2+cos(qy)^2*qx'^
         2)-dz'')*Bz>

   (121) %
   (122) % FORCES AND TORQUES
   (123) % ==================
   (124) A.AddTorque( N, Tx*ax> )        % Controlled revolute motor torque
-> (125) Torque_A_N> = Tx*Ax>

   (126) B.AddTorque( A, -Ty*ay> )       % Controlled revolute motor torque
-> (127) Torque_B_A> = -Ty*Ay>

   (128) Ecm.AddForce( Dcm, -Fz*bz> )    % Controlled prismatic motor torque
-> (129) Force_Ecm_Dcm> = -Fz*Bz>

   (130) %
   (131) % LAGRANGE EQUATION
   (132) % =================
   (133) Constant      kT                % Body wall, soft torsional stiffness
   (134) Constant      bT                % Dissipation, total torsional damping
   (135) Constant       b                % Dissipation, total linear damping
   (136) V = 1/2*kT*qx^2 + 1/2*kT*qy^2   % System potential energy
-> (137) V = 0.5*kT*(qx^2+qy^2)

   (138) K = System.GetKineticEnergy()   % System kinetic energy
-> (139) K = 0.5*I*qx'^2 + 1.5*J*qx'^2 + 1.5*J*qy'^2 + 1.5*(I-J)*sin(qy)^2*qx'^
         2 + 0.75*lLink^2*m*(qy'^2+cos(qy)^2*qx'^2) + 0.125*m*(4*dz'^2+(lLink-2
         *dz)^2*qy'^2+cos(qy)^2*(lLink-2*dz)^2*qx'^2)

   (140) Lagrangian = K - V
-> (141) Lagrangian = K - V

   (142) MechanicalEnergy = K + V
-> (143) MechanicalEnergy = V + K

   (144) SetGeneralizedSpeed( qx', qy', dz')
   (145) Qr = System.GetGeneralizedForce()
-> (146) Qr = [Tx; Ty; Fz]

   (147) Dissipation = 1/2*bT*( qx' )^2 + 1/2*bT*( qy' )^2 + 1/2*b*( dz' )^2 
-> (148) Dissipation = 0.5*b*dz'^2 + 0.5*bT*qx'^2 + 0.5*bT*qy'^2

   (149) Eqn[1] = Dt( D( K, qx' ) ) - D( Lagrangian, qx ) - Qr[1] + D( Dissipation, qx' )
-> (150) Eqn[1] = kT*qx + bT*qx' + 6*(I-J)*sin(qy)*cos(qy)*qy'*qx' + I*qx'' + 3
         *J*qx'' + 3*(I-J)*sin(qy)^2*qx'' - Tx - 1.5*lLink^2*m*cos(qy)*(2*sin(
         qy)*qy'*qx'-cos(qy)*qx'') - 0.25*m*cos(qy)*(lLink-2*dz)*(4*cos(qy)*dz'
         *qx'+2*sin(qy)*(lLink-2*dz)*qy'*qx'-cos(qy)*(lLink-2*dz)*qx'')

   (151) Eqn[2] = Dt( D( K, qy' ) ) - D( Lagrangian, qy ) - Qr[2] + D( Dissipation, qy' ) 
-> (152) Eqn[2] = kT*qy + bT*qy' + 3*J*qy'' + 1.5*lLink^2*m*qy'' - Ty - 0.25*
         sin(qy)*cos(qy)*(12*I-12*J-6*lLink^2*m-m*(lLink-2*dz)^2)*qx'^2 - 0.25*
         m*(lLink-2*dz)*(4*dz'*qy'-(lLink-2*dz)*qy'')

   (153) Eqn[3] = Dt( D( K, dz' ) ) - D( Lagrangian, dz ) - Qr[3] + D( Dissipation, dz' )
-> (154) Eqn[3] = b*dz' + 0.5*m*(lLink-2*dz)*(qy'^2+cos(qy)^2*qx'^2) + m*dz''
          - Fz

   (155) Solve( Eqn, qx'', qy'', dz'' )
-> (156) qx'' = -2*(2*kT*qx+2*bT*qx'+12*(I-J)*sin(qy)*cos(qy)*qy'*qx'-2*Tx-6*
         lLink^2*m*sin(qy)*cos(qy)*qy'*qx'-m*cos(qy)*(lLink-2*dz)*(2*cos(qy)*
         dz'+sin(qy)*(lLink-2*dz)*qy')*qx')/(4*I+12*J+6*lLink^2*m*cos(qy)^2+12*
         (I-J)*sin(qy)^2+m*cos(qy)^2*(lLink-2*dz)^2)

-> (157) qy'' = -(4*kT*qy+4*bT*qy'-4*Ty-4*m*(lLink-2*dz)*dz'*qy'-sin(qy)*cos(
         qy)*(12*I-12*J-6*lLink^2*m-m*(lLink-2*dz)^2)*qx'^2)/(12*J+6*lLink^2*m+
         m*(lLink-2*dz)^2)

-> (158) dz'' = (Fz-b*dz')/m - 0.5*(lLink-2*dz)*(qy'^2+cos(qy)^2*qx'^2)

   (159) %
   (160) % END EFFECTOR DESIRED PATH, DEFINITION
   (161) % =====================================
   (162) Specified                 x'       % Nx> measure, desired Eff position
   (163) Specified                 y'       % Ny> measure, desired Eff position
   (164) Specified                 z'       % Nz> measure, desired Eff position
   (165) Constant    palpateFrequency       % End effector palpating frequency
   (166) Constant    palpateAmplitude       % Amplitude of palpation
   (167) Constant        palpateDepth       % Depth below RCM of palpation
   (168) SetDt( x = 0 )
-> (169) x = 0
-> (170) x' = 0

   (171) SetDt( y = 0 )
-> (172) y = 0
-> (173) y' = 0

   (174) SetDt( z = -palpateDepth - palpateAmplitude*sin( palpateFrequency*t ) )
-> (175) z = -palpateDepth - palpateAmplitude*sin(palpateFrequency*t)
-> (176) z' = -palpateAmplitude*palpateFrequency*cos(palpateFrequency*t)

   (177) %
   (178) % END EFFECTOR DESIRED PATH, INVERSE KINEMATICS
   (179) % =============================================
   (180) Specified   qxDesired'         % Value of qx' that traces desired path
   (181) Specified   qyDesired'         % Value of qy' that traces desired path
   (182) Specified   dzDesired'         % Value of dz' that traces desired path
   (183) qxDesired = asin( y/sqrt( y^2 + z^2 ) )
-> (184) qxDesired = asin(y/sqrt(y^2+z^2))

   (185) qyDesired = asin( x/sqrt( x^2 + y^2 + z^2 ) )
-> (186) qyDesired = asin(x/sqrt(x^2+y^2+z^2))

   (187) dzDesired = sqrt( x^2 + y^2 + z^2 )
-> (188) dzDesired = sqrt(x^2+y^2+z^2)

   (189) eqnqx = sin( qxDesired ) - y/sqrt( y^2 + z^2 )
-> (190) eqnqx = sin(qxDesired) - y/sqrt(y^2+z^2)

   (191) eqnqy = sin( qyDesired ) - x/sqrt( x^2 + y^2 + z^2 )
-> (192) eqnqy = sin(qyDesired) - x/sqrt(x^2+y^2+z^2)

   (193) eqndz = dzDesired - sqrt( x^2 + y^2 + z^2 )
-> (194) eqndz = dzDesired - sqrt(x^2+y^2+z^2)

   (195) Solve( Dt( eqnqx ), qxDesired' )
-> (196) qxDesired' = -z*(y*z'-z*y')/(cos(qxDesired)*(y^2+z^2)^1.5)

   (197) Solve( Dt( eqnqy ), qyDesired' )
-> (198) qyDesired' = ((y^2+z^2)*x'-x*(y*y'+z*z'))/(cos(qyDesired)*(x^2+y^2+z^2
         )^1.5)

   (199) Solve( Dt( eqndz ), dzDesired' )
-> (200) dzDesired' = (x*x'+y*y'+z*z')/sqrt(x^2+y^2+z^2)

   (201) %
   (202) % PROPORTIONAL-DERIVATIVE (PD) CONTROL LAW
   (203) % ========================================
   (204) Constant           kpx         % Proportional control gain for qx
   (205) Constant           kdx         % Derivative control gain for qx'
   (206) Constant           kpy         % Proportional control gain for qy
   (207) Constant           kdy         % Derivative control gain for qy'
   (208) Constant           kpz         % Proportional control gain for dz
   (209) Constant           kdz         % Derivative control gain for dz'
   (210) Tx = kpx*( qxDesired - qx ) + kdx*( qxDesired' - qx' )
-> (211) Tx = kpx*(qxDesired-qx) + kdx*(qxDesired'-qx')

   (212) Ty = kpy*( qyDesired - qy ) + kdy*( qyDesired' - qy' )
-> (213) Ty = kpy*(qyDesired-qy) + kdy*(qyDesired'-qy')

   (214) Fz = kpz*( dzDesired - dz ) + kdz*( dzDesired' - dz' )
-> (215) Fz = kpz*(dzDesired-dz) + kdz*(dzDesired'-dz')

   (216) %
   (217) % ANIMATION
   (218) % =========
   (219) % Animated bodies F and G are parallel to bodies B and C,
   (220) % respectively. They are not necessary for computing the robot
   (221) % dynamics, but we include them here so that the simulation
   (222) % appears more realistic.
   (223) RigidBody        F         % Link 1p, Cylinder, with Points Fo, Fcm
   (224) RigidBody        G         % Link 2p, Cylinder, with Points Go, Gcm
   (225) Constant    lOffset        % Offset between parallel links, B-F and C-G
   (226) F.RotatePositiveZ( B, 0 )  % F is always parallel to B
-> (227) F_B = [1, 0, 0; 0, 1, 0; 0, 0, 1]
-> (228) w_F_B> = 0>
-> (229) w_F_N> = cos(qy)*qx'*Bx> - qy'*By> - sin(qy)*qx'*Bz>
-> (230) alf_F_B> = 0>
-> (231) alf_F_N> = (cos(qy)*qx''-sin(qy)*qx'*qy')*Bx> - qy''*By> + (-cos(qy)*
         qx'*qy'-sin(qy)*qx'')*Bz>

   (232) G.RotatePositiveZ( C, 0 )  % G is always parallel to C
-> (233) G_C = [1, 0, 0; 0, 1, 0; 0, 0, 1]
-> (234) w_G_C> = 0>
-> (235) w_G_N> = qx'*Cx>
-> (236) alf_G_C> = 0>
-> (237) alf_G_N> = qx''*Cx>

   (238) Fcm.Translate(Bcm,lOffset*ax>)
-> (239) p_Bcm_Fcm> = lOffset*Ax>
-> (240) v_Fcm_N> = -0.5*lLink*qy'*Bx> - 0.5*lLink*cos(qy)*qx'*By>
-> (241) a_Fcm_N> = -0.5*lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + 0.5*lLink*(2*
         sin(qy)*qx'*qy'-cos(qy)*qx'')*By> - 0.5*lLink*(qy'^2+cos(qy)^2*qx'^2)*
         Bz>

   (242) Gcm.Translate(Ccm,-lOffset*bz>)
-> (243) p_Ccm_Gcm> = -lOffset*Bz>
-> (244) v_Gcm_N> = -(lLink-lOffset)*qy'*Bx> - (lLink-lOffset)*cos(qy)*qx'*By>
-> (245) a_Gcm_N> = (lOffset*(sin(qy)*cos(qy)*qx'^2+qy'')-lLink*(sin(qy)*cos(
         qy)*qx'^2+qy''))*Bx> + (lLink*(2*sin(qy)*qx'*qy'-cos(qy)*qx'')-lOffset
         *(sin(qy)*qy'*qx'+sin(qy)*qx'*qy'-cos(qy)*qx''))*By> - (lLink-lOffset)
         *(qy'^2+cos(qy)^2*qx'^2)*Bz>

   (246) % Animation requires all body origins be defined
   (247) Ao.Translate(No,0>)
-> (248) p_No_Ao> = 0>
-> (249) v_Ao_N> = 0>
-> (250) a_Ao_N> = 0>

   (251) Bo.Translate(Bcm,0>)
-> (252) p_Bcm_Bo> = 0>
-> (253) v_Bo_N> = -0.5*lLink*qy'*Bx> - 0.5*lLink*cos(qy)*qx'*By>
-> (254) a_Bo_N> = -0.5*lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + 0.5*lLink*(2*
         sin(qy)*qx'*qy'-cos(qy)*qx'')*By> - 0.5*lLink*(qy'^2+cos(qy)^2*qx'^2)*
         Bz>

   (255) Co.Translate(Ccm,0>)
-> (256) p_Ccm_Co> = 0>
-> (257) v_Co_N> = -lLink*qy'*Bx> - lLink*cos(qy)*qx'*By>
-> (258) a_Co_N> = -lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + lLink*(2*sin(qy)*
         qx'*qy'-cos(qy)*qx'')*By> - lLink*(qy'^2+cos(qy)^2*qx'^2)*Bz>

   (259) Do.Translate(Dcm,0>)
-> (260) p_Dcm_Do> = 0>
-> (261) v_Do_N> = -0.5*lLink*qy'*Bx> - 0.5*lLink*cos(qy)*qx'*By>
-> (262) a_Do_N> = -0.5*lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + 0.5*lLink*(2*
         sin(qy)*qx'*qy'-cos(qy)*qx'')*By> - 0.5*lLink*(qy'^2+cos(qy)^2*qx'^2)*
         Bz>

   (263) Eo.Translate(Ecm,0>)
-> (264) p_Ecm_Eo> = 0>
-> (265) v_Eo_N> = -0.5*(lLink-2*dz)*qy'*Bx> - 0.5*cos(qy)*(lLink-2*dz)*qx'*By>
         - dz'*Bz>
-> (266) a_Eo_N> = (2*dz'*qy'+dz*sin(qy)*cos(qy)*qx'^2+dz*qy''-0.5*lLink*sin(
         qy)*cos(qy)*qx'^2-0.5*lLink*qy'')*Bx> + (2*cos(qy)*dz'*qx'+lLink*sin(
         qy)*qx'*qy'+dz*cos(qy)*qx''-2*dz*sin(qy)*qx'*qy'-0.5*lLink*cos(qy)*qx
         '')*By> + (dz*qy'^2+dz*cos(qy)^2*qx'^2-0.5*lLink*(qy'^2+cos(qy)^2*qx'^
         2)-dz'')*Bz>

   (267) Fo.Translate(Fcm,0>)
-> (268) p_Fcm_Fo> = 0>
-> (269) v_Fo_N> = -0.5*lLink*qy'*Bx> - 0.5*lLink*cos(qy)*qx'*By>
-> (270) a_Fo_N> = -0.5*lLink*(sin(qy)*cos(qy)*qx'^2+qy'')*Bx> + 0.5*lLink*(2*
         sin(qy)*qx'*qy'-cos(qy)*qx'')*By> - 0.5*lLink*(qy'^2+cos(qy)^2*qx'^2)*
         Bz>

   (271) Go.Translate(Gcm,0>)
-> (272) p_Gcm_Go> = 0>
-> (273) v_Go_N> = -(lLink-lOffset)*qy'*Bx> - (lLink-lOffset)*cos(qy)*qx'*By>
-> (274) a_Go_N> = (lOffset*(sin(qy)*cos(qy)*qx'^2+qy'')-lLink*(sin(qy)*cos(qy)
         *qx'^2+qy''))*Bx> + (lLink*(2*sin(qy)*qx'*qy'-cos(qy)*qx'')-lOffset*(
         sin(qy)*qy'*qx'+sin(qy)*qx'*qy'-cos(qy)*qx''))*By> - (lLink-lOffset)*(
         qy'^2+cos(qy)^2*qx'^2)*Bz>

   (275) Animate(N,No,B,C,D,E,F,G)
-> (276) P_No_Bo = [-0.5*lLink*sin(qy); -0.5*lLink*sin(qx)*cos(qy); 0.5*lLink*
         cos(qx)*cos(qy)]
-> (277) N_B[1,1] = cos(qy)
-> (278) N_B[1,2] = 0
-> (279) N_B[1,3] = -sin(qy)
-> (280) N_B[2,1] = -sin(qx)*sin(qy)
-> (281) N_B[2,2] = cos(qx)
-> (282) N_B[2,3] = -sin(qx)*cos(qy)
-> (283) N_B[3,1] = sin(qy)*cos(qx)
-> (284) N_B[3,2] = sin(qx)
-> (285) N_B[3,3] = cos(qx)*cos(qy)
-> (286) P_No_Co = [0.5*lLink*(1-2*sin(qy)); -lLink*sin(qx)*cos(qy); lLink*cos(
         qx)*cos(qy)]
-> (287) N_C = [1, 0, 0; 0, cos(qx), -sin(qx); 0, sin(qx), cos(qx)]
-> (288) P_No_Do = [-0.5*lLink*(-2+sin(qy)); -0.5*lLink*sin(qx)*cos(qy); 0.5*
         lLink*cos(qx)*cos(qy)]
-> (289) N_D[1,1] = cos(qy)
-> (290) N_D[1,2] = 0
-> (291) N_D[1,3] = -sin(qy)
-> (292) N_D[2,1] = -sin(qx)*sin(qy)
-> (293) N_D[2,2] = cos(qx)
-> (294) N_D[2,3] = -sin(qx)*cos(qy)
-> (295) N_D[3,1] = sin(qy)*cos(qx)
-> (296) N_D[3,2] = sin(qx)
-> (297) N_D[3,3] = cos(qx)*cos(qy)
-> (298) P_No_Eo[1] = lLink - 0.5*sin(qy)*(lLink-2*dz)
-> (299) P_No_Eo[2] = -0.5*sin(qx)*cos(qy)*(lLink-2*dz)
-> (300) P_No_Eo[3] = 0.5*cos(qx)*cos(qy)*(lLink-2*dz)
-> (301) N_E[1,1] = cos(qy)
-> (302) N_E[1,2] = 0
-> (303) N_E[1,3] = -sin(qy)
-> (304) N_E[2,1] = -sin(qx)*sin(qy)
-> (305) N_E[2,2] = cos(qx)
-> (306) N_E[2,3] = -sin(qx)*cos(qy)
-> (307) N_E[3,1] = sin(qy)*cos(qx)
-> (308) N_E[3,2] = sin(qx)
-> (309) N_E[3,3] = cos(qx)*cos(qy)
-> (310) P_No_Fo = [lOffset - 0.5*lLink*sin(qy); -0.5*lLink*sin(qx)*cos(qy); 
         0.5*lLink*cos(qx)*cos(qy)]
-> (311) N_F[1,1] = cos(qy)
-> (312) N_F[1,2] = 0
-> (313) N_F[1,3] = -sin(qy)
-> (314) N_F[2,1] = -sin(qx)*sin(qy)
-> (315) N_F[2,2] = cos(qx)
-> (316) N_F[2,3] = -sin(qx)*cos(qy)
-> (317) N_F[3,1] = sin(qy)*cos(qx)
-> (318) N_F[3,2] = sin(qx)
-> (319) N_F[3,3] = cos(qx)*cos(qy)
-> (320) P_No_Go[1] = 0.5*lLink - (lLink-lOffset)*sin(qy)
-> (321) P_No_Go[2] = -(lLink-lOffset)*sin(qx)*cos(qy)
-> (322) P_No_Go[3] = (lLink-lOffset)*cos(qx)*cos(qy)
-> (323) N_G = [1, 0, 0; 0, cos(qx), -sin(qx); 0, sin(qx), cos(qx)]

   (324) %
   (325) % INPUT VALUES
   (326) % ============
   (327) SetInput lLink = 0.5 m, rLink = 3 cm, lOffset = 10 cm
   (328) SetInput density = 2.7 g/(cm^3)
   (329) SetInput qx = 0 deg, qy = 0 deg, dz = 0.15 m
   (330) SetInput qx' = 0, qy' = 0, dz' = 0
   (331) SetInput kT = 10 N*m/rad, bT = 0.01 N*m*s/rad, b = 0.01 N*s/m
   (332) SetInput palpateAmplitude = 3 cm, palpateDepth = 15 cm
   (333) SetInput palpateFrequency = 0.5 rev/s
   (334) SetInput kpx = 1000 N*m, kdx = 50 N*m*s
   (335) SetInput kpy = 1000 N*m, kdy = 50 N*m*s
   (336) SetInput kpz = 500 N/m, kdz = 70 N*s/m
   (337) SetInput tfinal = 10 s, integStp = 0.02 s
   (338) %
   (339) % PLOTS AND OUTPUT
   (340) % ================
   (341) Ex = Dot(Eff.GetPosition(RCM),nx>)   % Nx> measure, Eff from RCM
-> (342) Ex = dz*sin(qy)

   (343) Ey = Dot(Eff.GetPosition(RCM),ny>)   % Ny> measure, Eff from RCM
-> (344) Ey = dz*sin(qx)*cos(qy)

   (345) Ez = Dot(Eff.GetPosition(RCM),nz>)   % Nz> measure, Eff from RCM
-> (346) Ez = -dz*cos(qx)*cos(qy)

   (347) % PlotOutput t s, Ex m, Ey m, Ez m
   (348) %
   (349) % SIMULATION SAVE
   (350) % ===============
   (351) ODE() Link
